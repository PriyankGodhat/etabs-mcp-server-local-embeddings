

CSI API ETABS v1

# Visual Basic for Applications (VBA) via Excel  
  
---  
  
  1. Create a new VBA project. 

  2. Add a reference to “ETABSv1.tlb” to the VBA project. This early binds to the API using the COM interface. “ETABSv1.tlb” can be found in the directory where ETABS is installed. 

  3. Add a new module to the VBA project and paste in the following, ready-to-run code. 

VB

Copy

         
         Sub Main()
         
           'set the following flag to True to attach to an existing instance of the program
           'otherwise a new instance of the program will be started
           Dim AttachToInstance As Boolean
           AttachToInstance = False
         
           'set the following flag to True to manually specify the path to ETABS.exe
           'this allows for a connection to a version of ETABS other than the latest installation
           'otherwise the latest installed version of ETABS will be launched
           Dim SpecifyPath As Boolean
           SpecifyPath = False
         
           'if the above flag is set to True, specify the path to ETABS below
           Dim ProgramPath As String
           'ProgramPath = "C:\Program Files\Computers and Structures\ETABS 19\ETABS.exe"
         
           'full path to the model
           'set it to the desired path of your model
           Dim ModelDirectory As String
           ModelDirectory = "C:\CSi_ETABS_API_Example"
           If Len(Dir(ModelDirectory, vbDirectory)) = 0 Then
             MkDir ModelDirectory
           End If
         
           Dim ModelName As String
           ModelName = "ETABS_API_Example.edb"
         
           Dim ModelPath As String
           ModelPath = ModelDirectory & Application.PathSeparator & ModelName
         
           'create API helper object
           Dim myHelper As ETABSv1.cHelper
           Set myHelper = New ETABSv1.Helper
         
           'dimension the ETABS Object as cOAPI type
           Dim myETABSObject As ETABSv1.cOAPI
           Set myETABSObject = Nothing
         
           'use ret to check return values of API calls
           Dim ret As Long
         
           If AttachToInstance Then
             'attach to a running instance of ETABS
             'get the active ETABS object
             Set myETABSObject = myHelper.GetObject("CSI.ETABS.API.ETABSObject")
           Else
             If SpecifyPath Then
               'create an instance of the ETABS object from the specified path
               Set myETABSObject = myHelper.CreateObject(ProgramPath)
             Else
               'create an instance of the ETABS object from the latest installed ETABS
               Set myETABSObject = myHelper.CreateObjectProgID("CSI.ETABS.API.ETABSObject")
             End If
             'start ETABS application
             myETABSObject.ApplicationStart
           End If
         
           'get a reference to cSapModel to access all API classes and functions
           Dim mySapModel As ETABSv1.cSapModel
           Set mySapModel = myETABSObject.SapModel
         
           'initialize model
           ret = mySapModel.InitializeNewModel()
         
           'create steel deck template model
           ret = mySapModel.File.NewSteelDeck(4, 12, 12, 4, 4, 24, 24)
         
           'save model
           ret = mySapModel.File.Save(ModelPath)
         
           'run analysis
           ret = mySapModel.Analyze.RunAnalysis
         
           'close ETABS
           myETABSObject.ApplicationExit (False)
         
           'clean up variables
           Set mySapModel = Nothing
           Set myETABSObject = Nothing
         
           If ret = 0 Then
             MsgBox "API script completed successfully."
           Else
             MsgBox "API script FAILED to complete."
           End If
         
           Exit Sub
         
           ErrHandler:
             MsgBox "Cannot run API script: " & Err.Description
         
         End Sub

ETABS®, SAFE®, SAP2000® and CSiBridge® are registered trademarks of Computers
and Structures, Inc.  

[Copyright © Computers and Structures, Inc. 2023. All rights
reserved.](http://www.csiamerica.com)

Send comments on this topic to
[support@csiamerica.com](mailto:support%40csiamerica.com?Subject=CSI%20API%20ETABS%20v1)

